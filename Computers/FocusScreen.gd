extends MeshInstance3D

@onready var clickbox =  $StaticBody3D
@onready var node_viewport = $SubViewport
## Used for checking if the mouse is inside the Area3D.
var is_mouse_inside: bool = false

## The last processed input touch/mouse event. Used to calculate relative movement.
var last_event_pos2D := Vector2()

## The time of the last event in seconds since engine start.
var last_event_time := -1.0
@onready var node_quad: MeshInstance3D = $Quad
@onready var node_area = $StaticBody3D
var lastplayer
# Called when the node enters the scene tree for the first time.
var focused = false

func _clicked(object,player):
	if object == clickbox:
		get_viewport().get_camera_3d().current = false
		$Camera.current = true
		player.release_mouse()
		lastplayer = player
		$ExtraUI.visible = true
		focused = true
		$SubViewport.get_children()[0].notify_focus_changed(true)
		

func _unhandled_input(event: InputEvent) -> void:
	if focused:
		if  Input.is_action_just_pressed("ui_cancel"): 
			_leave_focus()
		else:
			for mouse_event in [InputEventMouseButton, InputEventMouseMotion, InputEventScreenDrag, InputEventScreenTouch]:
				if is_instance_of(event, mouse_event):
					# If the event is a mouse/touch event, then we can ignore it here, because it will be
					# handled via Physics Picking.
					return
		node_viewport.push_input(event)
	
func _leave_focus():
	focused = false
	$ExtraUI.visible = false
	$SubViewport.get_children()[0].notify_focus_changed(false)
	lastplayer.recapture_camera()

func _on_exit_button_pressed() -> void:
	_leave_focus()


func _ready() -> void:
	node_area.mouse_entered.connect(_mouse_entered_area)
	node_area.mouse_exited.connect(_mouse_exited_area)
	node_area.input_event.connect(_mouse_input_event)


func _mouse_entered_area() -> void:
	is_mouse_inside = true
	# Notify the viewport that the mouse is now hovering it.
	node_viewport.notify_mouse_entered()


func _mouse_exited_area() -> void:
	# Notify the viewport that the mouse is no longer hovering it.
	node_viewport.notify_mouse_exited()
	is_mouse_inside = false




func _mouse_input_event(_camera: Camera3D, input_event: InputEvent, event_position: Vector3, _normal: Vector3, _shape_idx: int) -> void:
	if focused:
	# Get mesh size to detect edges and make conversions. This code only supports PlaneMesh and QuadMesh.
		var quad_mesh_size: Vector2 = node_quad.mesh.size

		# Event position in Area3D in world coordinate space.
		var event_pos3D := event_position

		# Current time in seconds since engine start.
		var now := Time.get_ticks_msec() / 1000.0

		# Convert position to a coordinate space relative to the Area3D node.
		# NOTE: `affine_inverse()` accounts for the Area3D node's scale, rotation, and position in the scene!
		event_pos3D = node_quad.global_transform.affine_inverse() * event_pos3D


		var event_pos2D := Vector2()

		
			# Convert the relative event position from 3D to 2D.
		event_pos2D = Vector2(event_pos3D.x, -event_pos3D.y)

		# Right now the event position's range is the following: (-quad_size/2) -> (quad_size/2)
		# We need to convert it into the following range: -0.5 -> 0.5
		event_pos2D.x = event_pos2D.x / quad_mesh_size.x
		event_pos2D.y = event_pos2D.y / quad_mesh_size.y
		# Then we need to convert it into the following range: 0 -> 1
		event_pos2D.x += 0.5
		event_pos2D.y += 0.5

		# Finally, we convert the position to the following range: 0 -> viewport.size
		event_pos2D.x *= node_viewport.size.x
		event_pos2D.y *= node_viewport.size.y
		# We need to do these conversions so the event's position is in the viewport's coordinate system.


		# Set the event's position and global position.
		input_event.position = event_pos2D
		if input_event is InputEventMouse:
			input_event.global_position = event_pos2D

		# Calculate the relative event distance.
		if input_event is InputEventMouseMotion or input_event is InputEventScreenDrag:
			# If there is not a stored previous position, then we'll assume there is no relative motion.
			if last_event_pos2D == null:
				input_event.relative = Vector2(0, 0)
			# If there is a stored previous position, then we'll calculate the relative position by subtracting
			# the previous position from the new position. This will give us the distance the event traveled from prev_pos.
			else:
				input_event.relative = event_pos2D - last_event_pos2D
				input_event.velocity = input_event.relative / (now - last_event_time)

		# Update last_event_pos2D with the position we just calculated.
		last_event_pos2D = event_pos2D

		# Update last_event_time to current time.
		last_event_time = now

		# Finally, send the processed input event to the viewport.
		node_viewport.push_input(input_event, true)
